apply plugin: 'assembler'
apply plugin: 'c'

model {
    buildTypes {
        debug
        release
    }
    toolChains {
        xgcc(Gcc) {
            target("tinkerPpc") {
                File srcDir = new File("src")
                cppCompiler.executable = "powerpc-eabi-gcc"
                cCompiler.executable = "powerpc-eabi-gcc"
                assembler.executable = "powerpc-eabi-as"
                linker.executable = "powerpc-eabi-gcc"
                staticLibArchiver.executable = "powerpc-eabi-ar"
                cCompiler.withArguments { args ->
                    args << "-I" + srcDir.getAbsolutePath() + "/arch/ppc32"
                    args << "-mcpu=powerpc"
                    args << "-DARCH_HAS_MMU"
                }
            }
            target("tinkerArmSoft") {
                File srcDir = new File("src")
                cppCompiler.executable = "arm-eabi-gcc"
                cCompiler.executable = "arm-eabi-gcc"
                assembler.executable = "arm-eabi-as"
                linker.executable = "arm-eabi-gcc"
                staticLibArchiver.executable = "arm-eabi-ar"
                cCompiler.withArguments { args ->
                    args << "-I" + srcDir.getAbsolutePath() + "/arch/arm"
                    args << "-fpic"
                    args << "-marm"
                    args << "-DVIRTUAL_ADDRESS_SPACE=0"
                    args << "-UARCH_HAS_MMU"
                }
            }
            target("tinkerArmHardVfp") {
                File srcDir = new File("src")
                cppCompiler.executable = "arm-eabi-gcc"
                cCompiler.executable = "arm-eabi-gcc"
                assembler.executable = "arm-eabi-as"
                linker.executable = "arm-eabi-gcc"
                staticLibArchiver.executable = "arm-eabi-ar"
                cCompiler.withArguments { args ->
                    args << "-I" + srcDir.getAbsolutePath() + "/arch/arm"
                    args << "-fpic"
                    args << "-marm"
                    args << "-DVIRTUAL_ADDRESS_SPACE=0"
                    args << "-UARCH_HAS_MMU"
                    args << "-mfpu=vfp"
                    args << "-mfloat-abi=hard"
                }
                linker.withArguments { args ->
                	args << "-mfpu=vfp"
                    args << "-mfloat-abi=hard"
                }
            }
        }
    }
    platforms {
        tinkerArmSoft {
            architecture "arm"
        }
        tinkerArmHardVfp {
            architecture "arm"
        }
        tinkerPpc {
            architecture "ppc"
        }
        windows_x86 {
        }
    }
    components {
    	// the base (headers)
    	base(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	targetPlatform "tinkerPpc"
	    	targetPlatform "windows_x86"
    		sources {
		    	c {
		            exportedHeaders {
		                srcDirs "src", "src/api"
		            }
		        }
	        }
    	}
    	// architectures
	    archArm(NativeLibrarySpec) {
	        targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	        sources {
		        c {
		            lib base.static
		            source {
		                srcDirs "src/arch/arm"
		                include "*.c"
		            }
		            exportedHeaders {
		                srcDir "src/arch/arm"
		            }
		        }
		        asm {
		            source {
		                srcDirs "src/arch/arm"
		                include "*.S"
		            }
		        }
	        }
	    }
	    archPowerPc(NativeLibrarySpec) {
	        targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	        sources {
		        c {
		            lib base.static
		            source {
		                srcDirs "src/arch/ppc32"
		                include "*.c"
		            }
		            exportedHeaders {
		                srcDir "src/arch/ppc32"
		            }
		        }
		        asm {
		            source {
		                srcDirs "src/arch/ppc32"
		                include "*.S"
		            }
		        }
	        }
	    }
	    // the core
	    core(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	targetPlatform "tinkerPpc"
	    	targetPlatform "windows_x86"
	    	sources {
			    c {
		             lib base.static
		             source {
		                 srcDirs "src/kernel"
		                 include "**/*.c"
		             }
		        }
	        }
	    }
	    // drivers
	    driverIntcOpic(NativeLibrarySpec) {
	    	targetPlatform "tinkerPpc"
	    	sources {
		    	c {
		    		lib base.static
		    		source {
		                 srcDirs "src/devices/intc/opic"
		                 include "**/*.c"
		             }
		    	}
	    	}
	    }
	    driverIntcBcm2835(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	sources {
		    	c {
		    		lib base.static
		    		source {
		                 srcDirs "src/devices/intc/bcm2835"
		                 include "**/*.c"
		             }
		    	}
	    	}
	    }
	    driverTimerBcm2835(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	sources {
		    	c {
		    		lib base.static
		    		source {
		                 srcDirs "src/devices/timer/bcm2835"
		                 include "**/*.c"
		             }
		    	}
	    	}
	    }
	    driverTimerOpic(NativeLibrarySpec) {
	    	targetPlatform "tinkerPpc"
	    	sources {
		    	c {
		    		lib base.static
		    		source {
		                 srcDirs "src/devices/timer/opic"
		                 include "**/*.c"
		             }
		    	}
	    	}
	    }
	    driverTimerPpc32tbr(NativeLibrarySpec) {
	    	targetPlatform "tinkerPpc"
	    	sources {
		    	c {
		    		lib base.static
		    		source {
		                 srcDirs "src/devices/timer/ppc32tbr"
		                 include "**/*.c"
		             }
		    	}
	    	}
	    }
	    driverSerialUart16550(NativeLibrarySpec) {
	    	targetPlatform "tinkerPpc"
	    	sources {
			    c {
					lib base.static
					source {
			             srcDirs "src/devices/serial/uart16550"
			             include "**/*.c"
			         }
				}
			}
	    }
	    driverSerialBcm2835(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	sources {
		    	c {
		    		lib base.static
		    		source {
		                 srcDirs "src/devices/serial/bcm2835"
		                 include "**/*.c"
		             }
		    	}
	    	}
	    }
	    // low-level
	    boot(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	targetPlatform "tinkerPpc"
	    	targetPlatform "windows_x86"
	    	sources {
			    c {
		             lib base.static
		             source {
		                 srcDir "src/boot"
		                 include "**/*.c"
		             }
		         }
	         }
	    }
	    // api
	    api(NativeLibrarySpec) {
	    	targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	    	targetPlatform "tinkerPpc"
	    	targetPlatform "windows_x86"
	    	sources {
			    c {
		             lib base.static
		             source {
		                 srcDir "src/api"
		                 include "**/*.c"
		             }
		         }
	         }
	    }
	    
	    armRaspPi(NativeExecutableSpec) {
	        //targetPlatform "tinkerArmSoft"
	    	targetPlatform "tinkerArmHardVfp"
	        binaries.all {
	            File srcDir = new File('src')
	            linker.args "-lgcc",
	                 "-T",
	                 (srcDir.getAbsolutePath() + "/arch/arm/pi/linker.ld")
	        }
	        sources {
		        c {
		            lib base.static        // base headers
		            lib api.static         // api layer
		            lib archArm.static     // generic arm code
		            lib boot.static        // boot c code into kernel
		            lib core.static        // the core kernel
		            lib driverTimerBcm2835.static
		            lib driverSerialBcm2835.static
		            lib driverIntcBcm2835.static
		            source {
		                srcDirs "src/arch/arm/pi", "src/tinker"
		                include "**/*.c"
		            }
		        }
		        asm {
		        	source {
		        		srcDirs "src/arch/arm/pi"
		        		include "**/*.S"
		        	}
		        }
	        }
	    }
	    ppc32Gdb(NativeExecutableSpec) {
	        targetPlatform "tinkerPpc"
	        binaries.all {
	            File srcDir = new File('src')
	            linker.args "-lgcc",
	                 "-T",
	                 (srcDir.getAbsolutePath() + "\\arch\\ppc32\\gdb_sim\\linker.ld")
	        }
	        sources {
		        c {
		            lib base.static        // base headers
		            lib api.static         // api layer
		            lib archPowerPc.static // powerpc code
		            lib boot.static        // boot c code into kernel
		            lib core.static        // the core kernel
		            lib driverSerialUart16550.static
		            lib driverTimerPpc32tbr.static
		            lib driverTimerOpic.static
		            lib driverIntcOpic.static
		            source {
		                srcDirs "src/arch/ppc32/gdb_sim", "src/arch/ppc32/generic", "src/tinker"
		                include "**/*.c"
		            }
		        }
	        }
	    }
	    
	    binaries.all {
	    	cCompiler.args "-Wno-maybe-uninitialized",
	            "-pedantic",
	            "-pedantic-errors",
	            "-Wall",
	            "-Wextra",
	            "-Werror",
	            "-c",
	            "-fmessage-length=0",
	            "-std=gnu11",
	            "-fno-builtin",
	            "-ffreestanding",
	            "-DBUILDING_KERNEL"
	        linker.args "-nostartfiles",
		        "-nodefaultlibs",
		        "-nostdlib",
		        "-static"
	        if (buildType == buildTypes.debug) {
	            cCompiler.args "-O0", 
	                "-g3",
	                "-gdwarf-4",
	                "-DKERNEL_SHELL",
	                "-UTIMER_DEBUGGING",
	                "-UINTC_DEBUGGING",
	                "-DKERNEL_DEBUGGING",
	                "-UMEMORY_DEBUGGING",
	                "-UDEBUG_COLLECTIONS",
	                "-UPROCESS_DEBUGGING",
	                "-USHM_DEBUGGING",
	                "-UTARGET_DEBUGGING",
	                "-USYSCALL_DEBUGGING",
	                "-UALARM_DEBUGGING",
	                "-UREGISTRY_DEBUGGING",
	                "-UPIPE_DEBUGGING",
	                "-UIDLE_DEBUGGING",
	                "-UKERNEL_SHELL_DEBUG"
	        } else if (buildType == buildTypes.release) {
	            cCompiler.args "-Os",
	                "-g0"
	            linker.args "-s"
	        }
	    }
    }
}


binaries.withType(SharedLibraryBinary) {
     buildable false
}

task debugPiBinary(type:Exec) {
  dependsOn binaries.withType(NativeExecutableBinary).matching {
    it.buildable && it.component.name.contains("armRaspPi") && it.buildType.equals(buildTypes.debug)
  }
  commandLine 'arm-eabi-objcopy', '-O', 'binary', 'build/binaries/armRaspPiExecutable/debug/armRaspPi.exe', 'build/binaries/armRaspPiExecutable/debug/kernel.img'
  standardOutput = new ByteArrayOutputStream()
  ext.output = {
    return standardOutput.toString()
  }
}

task releasePiBinary(type:Exec) {
  dependsOn binaries.withType(NativeExecutableBinary).matching {
    it.buildable && it.component.name.contains("armRaspPi") && it.buildType.equals(buildTypes.release)
  }
  commandLine 'arm-eabi-objcopy', '-O', 'binary', 'build/binaries/armRaspPiExecutable/release/armRaspPi.exe', 'build/binaries/armRaspPiExecutable/release/kernel.img'
  standardOutput = new ByteArrayOutputStream()
  ext.output = {
    return standardOutput.toString()
  }
}