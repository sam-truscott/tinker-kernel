apply plugin: 'assembler'
apply plugin: 'c'

model {
    buildTypes {
        debug
        release
    }
    platforms {
        tinkerArm {
            architecture "arm"
            operatingSystem "linux"
        }
        tinkerPowerPc {
            architecture "ppc"
            operatingSystem "linux"
        }
        tinkerX86 {
            architecture "x86"
            operatingSystem "linux"
        }
    }
    toolChains {
        gccPowerPc(Gcc) {
            getCppCompiler().setExecutable 'powerpc-eabi-gcc'
            getCCompiler().setExecutable 'powerpc-eabi-gcc'
            getAssembler().setExecutable 'powerpc-eabi-as'
            getLinker().setExecutable 'powerpc-eabi-gcc'
            getStaticLibArchiver().setExecutable 'powerpc-eabi-ar'
            addPlatformConfiguration(new PowerPcSupport())
        }
        gccX86(Gcc) {
            getCppCompiler().setExecutable 'i686-elf-gcc'
            getCCompiler().setExecutable 'i686-elf-gcc'
            getAssembler().setExecutable 'i686-elf-as'
            getLinker().setExecutable 'i686-elf-gcc'
            getStaticLibArchiver().setExecutable 'i686-elf-ar'
            addPlatformConfiguration(new X86Support())
        }
    }
}
    class PowerPcSupport implements TargetPlatformConfiguration {
        File srcDir = new File('src')

        boolean supportsPlatform(Platform element) {
            return element.getArchitecture().name == "ppc"
        }

        List<String> getCppCompilerArgs() {
            []
        }

        List<String> getObjectiveCppCompilerArgs() {
            []
        }

        List<String> getObjectiveCCompilerArgs() {
            []
        }

        List<String> getCCompilerArgs() {
            ["-I" + srcDir.getAbsolutePath() + "/arch/ppc32",
             "-nostdinc",
             "-mcpu=powerpc",
             "-pedantic",
             //"-pedantic-errors",
             "-Wall",
             "-Wextra",
             //"-Werror",
             "-c",
             "-fmessage-length=0",
             "-std=gnu99",
             "-fno-builtin",
             "-ffreestanding"]
        }

        List<String> getAssemblerArgs() {
            []
        }

        List<String> getLinkerArgs() {
            ["-nostartfiles",
             "-nodefaultlibs",
             "-nostdlib",
             "-static"]
        }

        List<String> getStaticLibraryArchiverArgs() {
            []
        }
    }

    class X86Support implements TargetPlatformConfiguration {
        File srcDir = new File('src')

        boolean supportsPlatform(Platform element) {
            return element.getArchitecture().name == "x86"
        }

        List<String> getCppCompilerArgs() {
            []
        }

        List<String> getObjectiveCppCompilerArgs() {
            []
        }

        List<String> getObjectiveCCompilerArgs() {
		[]
        }

        List<String> getCCompilerArgs() {
            ["-I" + srcDir.getAbsolutePath() + "/arch/x86",
             "-nostdinc",
             "-march=i686",
             "-pedantic",
             //"-pedantic-errors",
             "-Wall",
             "-Wextra",
             //"-Werror",
             "-c",
             "-fmessage-length=0",
             "-std=gnu99",
             "-fno-builtin",
             "-ffreestanding"]//,
             //"-fleading-underscore"]
             //"-fno-leading-underscore"]
        }

        List<String> getAssemblerArgs() {
            []
        }

        List<String> getLinkerArgs() {
            ["-nostartfiles",
             "-nodefaultlibs",
             "-nostdlib",
             "-static"]
        }

        List<String> getStaticLibraryArchiverArgs() {
            []
        }
    }

libraries {
    // the core kernel
    base {}
    // architectures
    archX86 {
        targetPlatforms "tinkerX86"
    }
    archArm6 {
        targetPlatforms "tinkerArm"
    }
    archPowerPc {
        targetPlatforms "tinkerPowerPc"
    }
    // the core
    core {}
    boot {}
    api {}
}

executables {
    armv6Pi {
        targetPlatforms "tinkerArm"
    }
    x86 {
        targetPlatforms "tinkerX86"
        binaries.all {
            File srcDir = new File('src')
            linker.args "-lgcc",
                 "-T",
                 (srcDir.getAbsolutePath() + "\\arch\\x86\\linker.ld")
        }
    }
    ppc32Gdb {
        targetPlatforms "tinkerPowerPc"
        binaries.all {
            File srcDir = new File('src')
            linker.args "-lgcc",
                 "-T",
                 (srcDir.getAbsolutePath() + "\\arch\\ppc32\\gdb_sim\\linker.ld")
        }
    }
}

sources {
    base {
        c {
            exportedHeaders {
                srcDirs "src", "src/api"
            }
        }
    }
    archX86 {
        c {
            lib libraries.base.static
            exportedHeaders {
                srcDir "src/arch/x86"
            }
        }
        asm {
            source {
                srcDirs "src/arch/x86"
                include "*.S"
            }
        }
    }
    archPowerPc {
        c {
            lib libraries.base.static
            source {
                srcDirs "src/arch/ppc32"
                include "*.c"
            }
            exportedHeaders {
                srcDir "src/arch/ppc32"
            }
        }
        asm {
            source {
                srcDirs "src/arch/ppc32"
                include "*.S"
            }
        }
    }
    api {
         c {
             lib libraries.base.static
             source {
                 srcDir "src/api"
                 include "**/*.c"
             }
         }
    }
    boot {
         c {
             lib libraries.base.static
             source {
                 srcDir "src/boot"
                 include "**/*.c"
             }
         }
    }
    core {
         c {
             lib libraries.base.static
             source {
                 srcDirs "src/kernel", "src/devices"
                 include "**/*.c"
             }
         }
    }
    ppc32Gdb {
        c {
            lib libraries.base.static        // core
            lib libraries.api.static         // api layer
            lib libraries.archPowerPc.static // powerpc code
            lib libraries.boot.static        // boot c code into kernel
            lib libraries.core.static        // the core kernel
            source {
                srcDirs "src/arch/ppc32/gdb_sim", "src/arch/ppc32/generic", "src/tinker"
                include "**/*.c"
            }
        }
    }
    x86 {
        c {
            lib libraries.base.static    // core
            lib libraries.api.static     // api layer
            lib libraries.archX86.static // x86 code
            lib libraries.boot.static    // boot c code into kernel
            lib libraries.core.static    // the core kernel
            source {
                srcDirs "src/tinker", "src/arch/x86"
                include "**/*.c"
            }
        }
    }
    binaries.all {
        if (buildType == buildTypes.debug) {
            cCompiler.args "-O0", "-g3", "-gdwarf-4", "-D__BUILDING_KERNEL","-D__KERNEL_SHELL", "-D__KERNEL_DEBUGGING", "-D__MEMORY_DEBUGGING","-D__DEBUG_COLLECTIONS","-D__PROCESS_DEBUGGING"
        } else if (buildType == buildTypes.release) {
            cCompiler.args "-Os", "-g0", "-gdwarf-4"
            linker.args "-s","-lgcc"
        }
    }
}